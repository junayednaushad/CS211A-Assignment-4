<script id="gouraud-shader-vs" type="x-shader/x-vertex">
    precision mediump float;
    
    attribute vec3 inPos;
    attribute vec3 inNV;
    attribute vec3 inCol;
    
    varying vec3 vertPos;
    varying vec3 vertNV;
    varying vec3 vertCol;
    
    uniform mat4 u_projectionMat44;
    uniform mat4 u_viewMat44;
    uniform mat4 u_modelMat44;
  
    struct TLightSource
    {
        vec3  lightDir;
        vec3  ambient;
        vec3  diffuse;
        vec3  specular;
        float shininess;
    };
  
    uniform TLightSource u_lightSource;
    
    vec3 Light( vec3 eyeV, vec3 N )
    {
        vec3  lightCol  = u_lightSource.ambient;
        vec3  L         = normalize( -u_lightSource.lightDir );
        float NdotL     = max( 0.0, dot( N, L ) );
        lightCol       += NdotL * u_lightSource.diffuse;
        vec3  H         = normalize( eyeV + L );
        float NdotH     = max( 0.0, dot( N, H ) );
        float kSpecular = ( u_lightSource.shininess + 2.0 ) * pow( NdotH, u_lightSource.shininess ) / ( 2.0 * 3.14159265 );
        lightCol       += kSpecular * u_lightSource.specular;
        return lightCol; 
    }
    
    void main()
    {
        vec3 modelNV  = mat3( u_modelMat44 ) * normalize( inNV );
        vertNV        = mat3( u_viewMat44 ) * modelNV;
        vec4 modelPos = u_modelMat44 * vec4( inPos, 1.0 );
        vec4 viewPos  = u_viewMat44 * modelPos;
        vertPos       = viewPos.xyz / viewPos.w;
        vec3 eyeV     = normalize( -vertPos );
        vec3 normalV  = normalize( vertNV );
        vertCol       = inCol * Light( eyeV, normalV );
        gl_Position   = u_projectionMat44 * viewPos;
    }
    </script>
    
    <script id="gouraud-shader-fs" type="x-shader/x-fragment">
    precision mediump float;
    
    varying vec3 vertPos;
    varying vec3 vertNV;
    varying vec3 vertCol;
    
    void main()
    {
        gl_FragColor = vec4( vertCol, 1.0 );
    }
    </script>
  
  <script id="phong-shader-vs" type="x-shader/x-vertex">
  precision mediump float;
  
  attribute vec3 inPos;
  attribute vec3 inNV;
  attribute vec3 inCol;
  
  varying vec3 vertPos;
  varying vec3 vertNV;
  varying vec3 vertCol;
  
  uniform mat4 u_projectionMat44;
  uniform mat4 u_viewMat44;
  uniform mat4 u_modelMat44;
  
  void main()
  {
    vec3 modelNV  = mat3( u_modelMat44 ) * normalize( inNV );
    vertNV        = mat3( u_viewMat44 ) * modelNV;
    vertCol       = inCol;
    vec4 modelPos = u_modelMat44 * vec4( inPos, 1.0 );
    vec4 viewPos  = u_viewMat44 * modelPos;
    vertPos       = viewPos.xyz / viewPos.w;
    gl_Position   = u_projectionMat44 * viewPos;
  }
  </script>
  
  <script id="phong-shader-fs" type="x-shader/x-fragment">
  precision mediump float;
  
  varying vec3 vertPos;
  varying vec3 vertNV;
  varying vec3 vertCol;
  
  struct TLightSource
  {
    vec3  lightDir;
    vec3  ambient;
    vec3  diffuse;
    vec3  specular;
    float shininess;
  };
  
  uniform TLightSource u_lightSource;
  
  vec3 Light( vec3 eyeV, vec3 N )
  {
    vec3  lightCol  = u_lightSource.ambient;
    vec3  L         = normalize( -u_lightSource.lightDir );
    float NdotL     = max( 0.0, dot( N, L ) );
    lightCol       += NdotL * u_lightSource.diffuse;
    vec3  H         = normalize( eyeV + L );
    float NdotH     = max( 0.0, dot( N, H ) );
    float kSpecular = ( u_lightSource.shininess + 2.0 ) * pow( NdotH, u_lightSource.shininess ) / ( 2.0 * 3.14159265 );
    lightCol       += kSpecular * u_lightSource.specular;
    return lightCol; 
  }
  
  void main()
  {
    vec3 eyeV    = normalize( -vertPos );
    vec3 normalV = normalize( vertNV );
    vec3 color   = vertCol * Light( eyeV, normalV );
    gl_FragColor = vec4( color, 1.0 );
  }
  </script>
  
  <form id="gui" name="inputs"><table><tr>
      <td><font color= #CCF>Shading:</font></td> 
      <td><select id="shading">>
          <option value="0">Gouraud</option>
          <option value="1">Phong</option>
      </select></td>
      </tr><tr>
      <td><font color= #CCF>Shininess:</font></td>
      <td><input type="range" id="shininess" min="0" max="100" value="20"/></td>
  </tr></table></form>
  <canvas id="canvas" style="border: none;" width="100%" height="100%"></canvas>
  